include(${PROJECT_SOURCE_DIR}/cmake/CheckHeader.txt)
include(${PROJECT_SOURCE_DIR}/cmake/CheckSize.txt)
include(${PROJECT_SOURCE_DIR}/cmake/CheckSymbol.txt)
include(${PROJECT_SOURCE_DIR}/cmake/Flags.txt)

find_package(Threads)

configure_file(${PROJECT_SOURCE_DIR}/src/config.h.in ${PROJECT_BINARY_DIR}/inc/config.h)

set(src_lib
    algo/anubis.c
    algo/anubis_sbox.c
    algo/cast6.c
    algo/cast6_sbox.c
    algo/rc6.c
    algo/ripemd.c
    algo/twofish.c
    algo/twofish_sbox.c
    algo/serpent.c
    algo/sha1.c
    algo/sha2.c
    algo/sha3.c
    algo/threefish_1024.c
    algo/camellia.c
    algo/camellia_sbox.c
    algo/rijndael.c
    algo/rijndael_sbox.c
    algo/tiger.c
    algo/tiger_sbox.c
    algo/whirlpool.c
    algo/whirlpool_sbox.c
    algo/blowfish.c
    algo/blowfish_sbox.c
    algo/seed.c
    algo/seed_sbox.c
    algo/skein.c
    algo/skein_transform_256.c
    algo/skein_transform_512.c
    algo/skein_transform_1024.c
    kdf/pbkdf2.c
    mode/ecb.c
    mode/cbc.c
    mode/hmac.c
    mode/ofb.c
    mode/ctr.c
    mode/cfb.c
    mode/cbc-mac.c
    mode/cmac.c
    xform/cipher.c
    xform/digest.c
    xform/mac.c
    akmos.c
    cipher.c
    digest.c
    mac.c)

set(src_amd64
    algo/amd64/threefish_256.s
    algo/amd64/threefish_512.s
    algo/amd64/sha3_transform.s
    algo/amd64/salsa_sse2.s
    algo/amd64/chacha_sse2.s
    amd64/pxor.s)

set(src_orig
    algo/threefish_256.c
    algo/threefish_512.c
    algo/sha3_transform.c
    algo/salsa.c
    algo/chacha.c
    pxor.c)

set(src_cli
    cli/main.c
    cli/cipher.c
    cli/digest.c
    cli/pw.c
    cli/mac.c)

if(${CMAKE_SYSTEM_PROCESSOR} MATCHES "x86_64")
    set(ARCH_AMD64 true)
elseif(${CMAKE_SYSTEM_PROCESSOR} MATCHES "amd64")
    set(ARCH_AMD64 true)
endif()

if(AKMOS_USE_ASM)
    if(ARCH_AMD64)
        enable_language(ASM)
        if(CMAKE_ASM_COMPILER_WORKS)
            list(SORT src_amd64)
            set_source_files_properties(${src_amd64} PROPERTIES COMPILE_FLAGS "-x assembler-with-cpp")
            add_library(akmos_obj_asm OBJECT ${src_amd64})
        endif()
    else()
        set(src_lib ${src_lib} ${src_orig})
    endif()
else()
    set(src_lib ${src_lib} ${src_orig})
endif()

list(SORT src_lib)
list(SORT src_cli)

include_directories("${PROJECT_BINARY_DIR}/inc")

set_source_files_properties(${src_lib} PROPERTIES COMPILE_DEFINITIONS "${C_DEFINE}")

add_library(akmos_obj OBJECT ${src_lib})
target_compile_options(akmos_obj PUBLIC ${C_FLAGS})

if(TARGET akmos_obj_asm)
    add_library(akmos_shared SHARED $<TARGET_OBJECTS:akmos_obj> $<TARGET_OBJECTS:akmos_obj_asm>)
    add_library(akmos_static STATIC $<TARGET_OBJECTS:akmos_obj> $<TARGET_OBJECTS:akmos_obj_asm>)
else()
    add_library(akmos_shared SHARED $<TARGET_OBJECTS:akmos_obj>)
    add_library(akmos_static STATIC $<TARGET_OBJECTS:akmos_obj>)
endif()

set_target_properties(akmos_shared PROPERTIES VERSION 0.6 SOVERSION 6 OUTPUT_NAME akmos)
set_target_properties(akmos_static PROPERTIES OUTPUT_NAME akmos)

add_executable(akmos ${src_cli})
target_compile_options(akmos PUBLIC ${C_FLAGS})
target_link_libraries(akmos akmos_shared ${CMAKE_THREAD_LIBS_INIT})

set(BINDIR bin)

install(TARGETS akmos_shared akmos_static akmos
        LIBRARY DESTINATION lib
        ARCHIVE DESTINATION lib
        RUNTIME DESTINATION bin)
install(FILES akmos.h
        DESTINATION include)
